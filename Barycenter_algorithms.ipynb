{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "barycenter_functional (generic function with 1 method)"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# INPUT:\n",
    "#     nIter = Number of iterations\n",
    "#     covs = d × d × n array containing covariance matrices\n",
    "#     sqrt_covs = d × d × n array containing square roots of matrices in covs\n",
    "#     objective = length nIter vector to save barycenter objective values in\n",
    "#     times = length nIter vector to save timings in\n",
    "#     η = step size\n",
    "#     distances = length nIter vector to save (W₂)² distance to (sqrt_best)² over training\n",
    "#     sqrt_best = square root of a d × d matrix that we calculate distances to throughout training (ideally taken to be the true barycenter)\n",
    "# OUTPUT:\n",
    "#     square root of d × d matrix that achieves best barycenter functional throughout training\n",
    "function GD!(nIter, covs, sqrt_covs, objective, times, η, distances, sqrt_best)\n",
    "    start = time()\n",
    "    d = size(covs)[1]\n",
    "    n = size(covs)[3]\n",
    "    X = zeros(d, d)\n",
    "    X .= covs[:,:,1]\n",
    "    \n",
    "    # Cache variables for memory efficiency. T refers to transport map\n",
    "    T = zeros(d,d)\n",
    "    evals = zeros(d)\n",
    "    evecs = zeros(d,d)\n",
    "    \n",
    "    bestval = Inf\n",
    "    candidate_best = zeros(d,d)\n",
    "    \n",
    "\n",
    "    for i in 1:nIter\n",
    "        T .= zeros(d,d)\n",
    "\n",
    "        for j in 1:n\n",
    "            sq = @view sqrt_covs[:,:,j]\n",
    "\n",
    "            e = eigen(Symmetric(sq*X*sq))\n",
    "            evals .= e.values\n",
    "            evecs .= e.vectors\n",
    "            objective[i] += tr(covs[:,:,j].-2*evecs*diagm(evals.^(0.5))*evecs')\n",
    "\n",
    "            T .= T .+ sq*evecs*diagm(evals.^(-0.5))*evecs'*sq\n",
    "        end\n",
    "        \n",
    "        objective[i] = objective[i]/n + tr(X)\n",
    "        if objective[i] < bestval\n",
    "            candidate_best .= X\n",
    "            bestval = objective[i]\n",
    "        end\n",
    "        \n",
    "        T .= T./n\n",
    "        distances[i] = bures(sqrt_best, X)\n",
    "        X .= Symmetric(((1 - η).*I(d) .+ η.*T)*X*((1-η).*I(d) + η.*T))\n",
    "        times[i] = time()-start\n",
    "    end\n",
    "    return candidate_best^.5\n",
    "end\n",
    "\n",
    "# INPUT:\n",
    "#     nIter = Number of iterations\n",
    "#     covs = d × d × n array containing covariance matrices\n",
    "#     sqrt_covs = d × d × n array containing square roots of matrices in covs\n",
    "#     objective = length nIter vector to save barycenter objective values in\n",
    "#     times = length nIter vector to save timings in\n",
    "#     η = step size\n",
    "#     α = lower eigenvalue to threshold at (should be ∼ average minimum eigenvalue of covs)\n",
    "#     β = upper eigenvalue to threshold at (should be ∼ average maximum eigenvalue of covs)\n",
    "#     distances = length nIter vector to save (W₂)² distance to (sqrt_best)² over training\n",
    "#     sqrt_best = square root of a d × d matrix that we calculate distances to throughout training (ideally taken to be the true barycenter)\n",
    "function EGD!(nIter, covs, sqrt_covs, objective, times, η, α, β, distances, sqrt_best)\n",
    "    start = time()\n",
    "    d = size(covs)[1]\n",
    "    n = size(covs)[3]\n",
    "    \n",
    "    X = zeros(d, d)\n",
    "    X .= covs[:,:,1]\n",
    "    \n",
    "    # Cache variables for memory efficiency. T refers to transport map\n",
    "    T = zeros(d,d)\n",
    "    evals = zeros(d)\n",
    "    evecs = zeros(d,d)\n",
    "    \n",
    "    \n",
    "    for i in 1:nIter\n",
    "        \n",
    "        T .= zeros(d,d)\n",
    "\n",
    "        for j in 1:n\n",
    "            sq = @view sqrt_covs[:,:,j]\n",
    "            \n",
    "            e = eigen(Symmetric(sq*X*sq))\n",
    "            evals .= e.values\n",
    "            evecs .= e.vectors\n",
    "            objective[i] += tr(covs[:,:,j].-2*evecs*diagm(evals.^(0.5))*evecs')\n",
    "\n",
    "            T .= T .+ sq*evecs*diagm(evals.^(-0.5))*evecs'*sq\n",
    "        end\n",
    "    \n",
    "        objective[i] = objective[i]/n + tr(X)\n",
    "        times[i] = time()-start\n",
    "        distances[i] = bures(sqrt_best, X)\n",
    "        \n",
    "        T .= T./n\n",
    "        X .= Symmetric(X .- η.*(I(d) .- T))\n",
    "        \n",
    "        clip!(X, α, β)\n",
    "    end\n",
    "end\n",
    "\n",
    "# INPUT\n",
    "#     covs = d × d × n array containing covariance matrices\n",
    "#     sqrt_covs = d × d × n array containing square roots of matrices in covs\n",
    "#     X = starting covariance matrix \n",
    "#     objective = length nIter vector to save barycenter objective values in\n",
    "#     times = length nIter vector to save timings in\n",
    "#     ηs = array of length n of stepsizes, or single number (in which case that step size is used for all steps)\n",
    "#     sqrt_bary = square root of true barycenter\n",
    "function SGD!(covs, sqrt_covs, X, objective, times, ηs; sqrt_bary = nothing)\n",
    "    start = time()\n",
    "    d = size(covs)[1]\n",
    "    n = size(covs)[3]\n",
    "    if isnothing(sqrt_bary)\n",
    "        sqrt_bary = I(d)\n",
    "    end\n",
    "    \n",
    "    # Cache variables for memory efficiency. T refers to transport map\n",
    "    T = zeros(d,d)\n",
    "    evals = zeros(d)\n",
    "    evecs = zeros(d,d)\n",
    "\n",
    "    for i in 1:n\n",
    "        if length(ηs) == 1\n",
    "            η = ηs[1] \n",
    "        else\n",
    "            η = ηs[i]\n",
    "        end\n",
    "        sq = @view sqrt_covs[:,:,i]\n",
    "        e = eigen(Symmetric(sq*X*sq))\n",
    "        evals .= e.values\n",
    "        evecs .= e.vectors\n",
    "        T .= sq*evecs*diagm(evals.^(-0.5))*evecs'*sq\n",
    "        times[i] = time()-start\n",
    "        objective[i] = bures(sqrt_bary, X)\n",
    "        X .= Symmetric(((1 - η).*I(d) .+ η.*T)*X*((1-η).*I(d) + η.*T))\n",
    "    end\n",
    "end\n",
    "\n",
    "\n",
    "# INPUT\n",
    "#     covs = d × d × n array containing covariance matrices\n",
    "#     sqrt_covs = d × d × n array containing square roots of matrices in covs\n",
    "#     objective = length nIter vector to save barycenter objective values in\n",
    "#     times = length nIter vector to save timings in\n",
    "#     ηs = array of length n of stepsizes, or single number (in which case that step size is used for all steps)\n",
    "#     α = lower eigenvalue to threshold at (should be ∼ average minimum eigenvalue of covs)\n",
    "#     β = upper eigenvalue to threshold at (should be ∼ average maximum eigenvalue of covs)\n",
    "function ESGD!(covs, sqrt_covs, objective, times, ηs, α, β)\n",
    "    start = time()\n",
    "    d = size(covs)[1]\n",
    "    n = size(covs)[3]\n",
    "    \n",
    "    X = zeros(d, d)\n",
    "    X .= covs[:,:,1]\n",
    "    \n",
    "    # Cache variables for memory efficiency. T refers to transport map\n",
    "    T = zeros(d,d)\n",
    "    evals = zeros(d)\n",
    "    evecs = zeros(d,d)\n",
    "    \n",
    "    \n",
    "    for i in 1:n\n",
    "        if length(ηs) == 1\n",
    "            η = ηs[1] \n",
    "        else\n",
    "            η = ηs[i]\n",
    "        end\n",
    "        sq = @view sqrt_covs[:,:,i]\n",
    "        e = eigen(Symmetric(sq*X*sq))\n",
    "        evals .= e.values\n",
    "        evecs .= e.vectors\n",
    "        T .= sq*evecs*diagm(evals.^(-0.5))*evecs'*sq\n",
    "        times[i] = time()-start\n",
    "        objective[i] = bures(I(d), X)\n",
    "        X .= Symmetric(X .- η.*(I(d) .- T))\n",
    "        \n",
    "        clip!(X, α, β)\n",
    "    end\n",
    "end\n",
    "\n",
    "# INPUT\n",
    "#     covs = d × d × n array containing covariance matrices\n",
    "#     verbose = boolean indicating whether SDP solver should be verbose\n",
    "#     maxIter = maximum number of iterations\n",
    "# OUTPUT\n",
    "#     the barycenter (d × d matrix)\n",
    "function SDP(covs; verbose=false, maxIter = 5000)\n",
    "    d = size(covs)[1]\n",
    "    n = size(covs)[3]\n",
    "    \n",
    "    Σ = Variable(d,d)\n",
    "    Ss = [Variable(d,d) for _ in 1:n]\n",
    "    constr = [([covs[:,:,i] Ss[i]; Ss[i]' Σ] ⪰ 0) for i in 1:n]\n",
    "    problem = minimize(tr(Σ) - 2*mean(tr.(Ss)))\n",
    "    problem.constraints += constr\n",
    "    problem.constraints += (Σ ⪰ 0)\n",
    "    optimizer = SCS.Optimizer(verbose = verbose)\n",
    "    MOI.set(optimizer, MOI.RawParameter(\"max_iters\"), maxIter)\n",
    "    solve!(problem, optimizer)\n",
    "    return Σ.value\n",
    "end\n",
    "\n",
    "\n",
    "# Function that clips eigenvalues of X to specified range, in place\n",
    "function clip!(X, α, β)\n",
    "    e = eigen(X)\n",
    "    X .= Symmetric(e.vectors*diagm(clamp.(e.values, α, β))*e.vectors')\n",
    "end\n",
    "\n",
    "# Calculates (W₂)² between (sq)^2 and x\n",
    "function bures(sq,x)\n",
    "    e = eigen(sq*x*sq)\n",
    "    return tr(x+sq*sq- 2 .* e.vectors*diagm(e.values.^(.5))*e.vectors')\n",
    "end\n",
    "\n",
    "# Calculates barycenter functional of X over the dataset [sqrt_covs[:,:,i]² for i in 1:n]\n",
    "function barycenter_functional(sqrt_covs, X)\n",
    "    return mean(bures(sqrt_covs[:,:,i], X) for i in 1:size(sqrt_covs)[3])\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.1",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
